# Linear arrival rate, linear (site-specific) detection rate
# Can be used for both common predictors and separate predictors.
model {
  # priors
  # for the arrival rate regression
  alpha_arrival ~ dnorm(15, 1)
  beta_arrival ~ dnorm(15, 1)
  # for the detection probability regression
  alpha_detection ~ dnorm(0, 1)
  beta_detection ~ dnorm(0, 1)
  # standard deviation site effect
  sigma.eta ~ dt(0, 1, 4)T(0,)

  # models
  for (i in 1:n) {
    # model for the arrival process
    N[i] ~ dpois(exp(log_lambda[i]))
    # model for the detection process
    count[i] ~ dbinom(p[i], N[i])

    # regression for arrival process
    log_lambda[i] <- alpha_arrival + beta_arrival * x_arrival[i]
    # regression for detection process
    logit(p[i]) <- alpha_detection + beta_detection * x_detection[i] + eta[site[i]]
  }

  # site coefficients
  tau.eta <- pow(sigma.eta, -2)
  for (j in 1:nsite) {
    eta[j] ~ dnorm(0, tau.eta)
  }
}
